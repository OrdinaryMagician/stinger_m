Class Bonesaw : UnrealWeapon
{
	override bool TryPickup( in out Actor toucher )
	{
		if ( !sting_proto || !sting_dubious ) return false;	// not allowed
		return Super.TryPickup(toucher);
	}
	override void Tick()
	{
		Super.Tick();
		if ( sting_proto && sting_dubious ) return;
		if ( !Owner )
		{
			let r = Spawn("Chainsaw",pos,ALLOW_REPLACE);
			r.spawnangle = spawnangle;
			r.spawnpoint = spawnpoint;
			r.angle = angle;
			r.pitch = pitch;
			r.roll = roll;
			r.special = special;
			r.args[0] = args[0];
			r.args[1] = args[1];
			r.args[2] = args[2];
			r.args[3] = args[3];
			r.args[4] = args[4];
			r.ChangeTid(tid);
			r.SpawnFlags = SpawnFlags&~MTF_SECRET;
			r.HandleSpawnFlags();
			r.SpawnFlags = SpawnFlags;
			r.bCountSecret = SpawnFlags&MTF_SECRET;
			r.vel = vel;
			r.master = master;
			r.target = target;
			r.tracer = tracer;
			r.bDropped = bDropped;
			r.bNeverRespawn = bNeverRespawn;
		}
		else Owner.RemoveInventory(self);
		Destroy();
	}
	override String GetObituary( Actor victim, Actor inflictor, Name mod, bool playerattack )
	{
		if ( !bAltFire ) return StringTable.Localize("$O_RAZORCLAW2");
		return StringTable.Localize((mod=='Decapitated')?"$O_RAZORCLAWDECAP":"$O_RAZORCLAW1");
	}
	private action bool TryHit( double angle, int dmg, bool decap = false )
	{
		FTranslatedLineTarget t;
		double slope = AimLineAttack(angle,DEFMELEERANGE,t,0.,ALF_CHECK3D);
		FLineTraceData d;
		Vector3 x, y, z, origin;
		[x, y, z] = dt_CoordUtil.GetAxes(pitch,angle,roll);
		origin = level.Vec3Offset(Vec2OffsetZ(0,0,player.viewz),y*4-z*4);
		LineTrace(angle,DEFMELEERANGE,slope,TRF_ABSPOSITION,origin.z,origin.x,origin.y,data:d);
		if ( d.HitType != TRACE_HitNone )
		{
			if ( d.HitType == TRACE_HitActor )
			{
				invoker.bFORCEPAIN = !Random[Bonesaw](0,d.HitActor.bBOSS?5:2);
				if ( decap && (d.HitLocation.z >= (d.HitActor.pos.z+d.HitActor.height*0.8)) )
					dmg = d.HitActor.DamageMobj(invoker,self,dmg*2,'Decapitated',DMG_USEANGLE|DMG_THRUSTLESS,atan2(d.HitDir.y,d.HitDir.x));
				else dmg = d.HitActor.DamageMobj(invoker,self,dmg,'slashed',DMG_USEANGLE|DMG_THRUSTLESS,atan2(d.HitDir.y,d.HitDir.x));
				UTMainHandler.DoKnockback(d.HitActor,d.HitDir,-7000);
				UTMainHandler.DoKnockback(self,-d.HitDir,-2000);
				if ( d.HitActor.player ) d.HitActor.A_QuakeEx(2,2,2,6,0,1,"",QF_RELATIVE|QF_SCALEDOWN,rollIntensity:0.25);
				if ( !d.HitActor.bNOBLOOD )
				{
					d.HitActor.TraceBleed(dmg,invoker);
					d.HitActor.SpawnBlood(d.HitLocation,atan2(d.HitDir.y,d.HitDir.x)+180,dmg);
				}
			}
			else if ( d.HitType == TRACE_HitWall )
				d.HitLine.RemoteActivate(self,d.LineSide,SPAC_Impact,d.HitLocation-d.HitDir*4);
			A_QuakeEx(1,1,1,3,0,1,"",QF_RELATIVE|QF_SCALEDOWN,rollIntensity:0.12);
			if ( !d.HitActor || d.HitActor.bNOBLOOD )
			{
				A_StartSound("ripper/hit",CHAN_WEAPON,CHANF_OVERLAP);
				let p = Spawn("SawImpact",d.HitLocation-d.HitDir*4);
				p.angle = atan2(d.HitDir.y,d.HitDir.x);
				p.pitch = asin(-d.HitDir.z);
			}
			else A_StartSound("ripper/flesh",CHAN_WEAPON,CHANF_OVERLAP);
			if ( invoker.bAltFire ) A_QuakeEx(3,3,3,3,0,1,"",QF_RELATIVE|QF_SCALEDOWN,rollIntensity:0.13);
			else A_QuakeEx(1,1,1,6,0,1,"",QF_RELATIVE|QF_SCALEDOWN,rollIntensity:0.06);
			return true;
		}
		return false;
	}
	action void A_Slice()
	{
		invoker.special1++;
		Vector3 x, y, z, origin;
		[x, y, z] = dt_CoordUtil.GetAxes(pitch,angle,roll);
		origin = level.Vec3Offset(Vec2OffsetZ(0,0,player.viewz),x*15+y*4-z*4);
		if ( waterlevel >= 3 )
		{
			UTMainHandler.DoKnockback(self,x,5000);
			for ( int i=0; i<3; i++ )
			{
				let b = Spawn("UTBubble",level.Vec3Offset(origin,FRandom[Bonesaw](-4,4)*y+FRandom[Bonesaw](-4,4)*z));
				b.scale *= .5;
				b.vel += vel*.5;
			}
		}
		if ( invoker.special1 < 4 ) return;
		invoker.special1 = 0;
		invoker.FireEffect();
		A_AlertMonsters();
		for ( int i=0; i<8; i++ ) if ( TryHit(angle+i*(45./16),10) || TryHit(angle-i*(45./16),10) ) return;
	}
	action void A_Clamp()
	{
		invoker.FireEffect();
		A_AlertMonsters();
		Vector3 x, y, z, origin;
		[x, y, z] = dt_CoordUtil.GetAxes(pitch,angle,roll);
		origin = level.Vec3Offset(Vec2OffsetZ(0,0,player.viewz),x*15+y*4-z*4);
		if ( waterlevel >= 3 )
		{
			UTMainHandler.DoKnockback(self,x,20000);
			for ( int i=0; i<12; i++ )
			{
				let b = Spawn("UTBubble",level.Vec3Offset(origin,FRandom[Bonesaw](-4,4)*y+FRandom[Bonesaw](-4,4)*z));
				b.scale *= .5;
				b.vel += vel*.5;
			}
		}
		for ( int i=0; i<8; i++ ) if ( TryHit(angle+i*(45./16),30,true) || TryHit(angle-i*(45./16),30,true) ) return;
	}
	Default
	{
		Tag "$T_RAZORCLAW";
		Inventory.Icon "I_BSaw";
		Inventory.PickupMessage "$I_RAZORCLAW";
		Weapon.UpSound "bonesaw/select";
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 5500;
		Weapon.SlotPriority 0.9;
		+WEAPON.MELEEWEAPON;
	}
	States
	{
	Spawn:
		CSWP A -1;
		Stop;
		CSWP B -1;
		Stop;
	Select:
		CSWS A 1 A_Raise(int.max);
		Wait;
	Ready:
		CSWS ABCDEFGHI 2 A_WeaponReady(WRF_NOFIRE);
		Goto Idle;
	Dummy:
		TNT1 A 1 A_WeaponReady();
		Wait;
	Idle:
		#### # 2 A_Overlay(-9999,"Dummy");
		CSWI ABCDE 15;
		CSWI A 0 A_Jump(32,"Twiddle");
		Goto Idle+1;
	Twiddle:
		#### # 2;
		CSWT ABCDEFGHIJKLM 3;
		CSWI A 3;
		Goto Idle;
	Fire:
		#### # 2
		{
			A_Overlay(-9999,"Null");
			A_StartSound("bonesaw/spin",CHAN_WEAPONMISC,CHANF_LOOPING,Dampener.Active(self)?.1:1.);
			invoker.special2 = 0;
		}
		CSWF ABCDEFGHIJKLMNO 1
		{
			invoker.special2++;
			A_WeaponOffset(0+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,32+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,WOF_INTERPOLATE);
		}
		CSWF PQRSTUVWX 1
		{
			invoker.special2++;
			A_WeaponOffset(0+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,32+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,WOF_INTERPOLATE);
			A_Slice();
		}
		Goto Hold;
	Hold:
		CSWH A 0 A_SoundVolume(CHAN_WEAPONMISC,Dampener.Active(self)?.1:1.);
		CSWH ABCDEFGHIJKLMNOPQR 1
		{
			A_WeaponOffset(0+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,32+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,WOF_INTERPOLATE);
			A_Slice();
		}
		CSWH A 0 A_Refire("Hold");
		Goto Release;
	Release:
		CSWR A 0
		{
			A_StopSound(CHAN_WEAPONMISC);
			A_StartSound("bonesaw/spinend",CHAN_WEAPON,CHANF_OVERLAP,Dampener.Active(self)?.1:1.);
		}
		CSWR ABCDEFGHI 1
		{
			A_WeaponOffset(0+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,32+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,WOF_INTERPOLATE);
			invoker.special2--;
			A_Slice();
		}
		CSWR JKLMNOPQRSTUVWX 1
		{
			A_WeaponOffset(0+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,32+FRandom[Bonesaw](-0.1,0.1)*invoker.special2,WOF_INTERPOLATE);
			invoker.special2--;
		}
		Goto Idle;
	AltFire:
		#### # 1
		{
			A_Overlay(-9999,"Null");
			A_StartSound("bonesaw/claw",CHAN_WEAPON,CHANF_OVERLAP,Dampener.Active(self)?.1:1.);
		}
		CSWA ABC 2;
		CSWA DEF 1;
		CSWA G 0 A_Clamp();
		CSWA GH 1;
		CSWA IJLMN 3;
		CSWI A 3;
		Goto Idle;
	Deselect:
		CSWD A 1 A_Overlay(-9999,"Null");
		CSWD ABCDEFGHI 1;
		CSWD I 1 A_Lower(int.max);
		Wait;
	}
}
